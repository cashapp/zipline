import com.android.build.gradle.internal.tasks.AndroidTestTask

apply plugin: 'com.android.application'

buildscript {
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.2'
  }
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    applicationId 'com.squareup.duktape.tests'
    minSdkVersion 15
    targetSdkVersion 25

    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
  compile project(':duktape')
  androidTestCompile project(path: ':duktape')
  compile 'com.squareup.okio:okio:1.6.0'

  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.google.truth:truth:0.27'

  // Force tests to use the same version of support-annotations as our Duktape artifact.
  androidTestCompile 'com.android.support:support-annotations:25.0.0'
}

tasks.withType(AndroidTestTask) { task ->
  task.doFirst {
    logging.level = LogLevel.INFO
  }
  task.doLast {
    logging.level = LogLevel.LIFECYCLE
  }
}
